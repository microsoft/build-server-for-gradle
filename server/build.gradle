plugins {
    id 'application'
}

dependencies {
    implementation project(':model')
    implementation 'org.gradle:gradle-tooling-api:8.1.1'
    implementation 'ch.epfl.scala:bsp4j:2.1.0-M4'
    implementation 'ch.qos.logback:logback-core:1.4.6'
    implementation 'ch.qos.logback:logback-classic:1.4.6'
    implementation 'org.slf4j:slf4j-api:2.0.7'

    testImplementation(platform('org.junit:junit-bom:5.9.2'))
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core:5.3.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.0'
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
}

application {
    mainClass = 'com.microsoft.java.bs.core.Launcher'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

task generateInitScript() {
  doLast {
    def initScript = file("$buildDir/libs/plugins/init.gradle")
    initScript.parentFile.mkdirs()
    initScript.write """
      initscript {
        dependencies {
          classpath files('plugin.jar')
        }
      }
      allprojects {
        afterEvaluate {
          it.getPlugins().apply(com.microsoft.java.bs.gradle.plugin.GradleBuildServerPlugin)
        }
      }
      """
  }
}

task copyRuntimeLibs(type: Copy) {
  from configurations.runtimeClasspath
  into "$buildDir/libs/runtime"
}

processResources {
  dependsOn(':plugin:copyJar')
  dependsOn copyRuntimeLibs
  dependsOn(':server:generateInitScript')
  duplicatesStrategy = 'include'
}